cmake_minimum_required(VERSION 3.21)
project(ProceduralAnimation LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(FetchContent)
include(InstallRequiredSystemLibraries)

if(NOT WIN32 AND NOT CMAKE_BUILD_TYPE)
    message(STATUS "No build type specified; defaulting to Release.")
    set(CMAKE_BUILD_TYPE Release CACHE STRING
        "Choose the build type (Debug, Release, RelWithDebInfo, MinSizeRel)." FORCE)
    set(CMAKE_CXX_FLAGS_RELEASE "-O2 -march=native -DNDEBUG")
    set(CMAKE_CXX_FLAGS_DEBUG "-O0 -g")
endif()

FetchContent_Declare(
    sfml
    GIT_REPOSITORY https://github.com/SFML/SFML.git
    GIT_TAG        2.6.2 
)
set(SFML_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(SFML_BUILD_TEST_SUITE OFF CACHE BOOL "" FORCE)
FetchContent_MakeAvailable(sfml)

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/ProceduralAnimation/bin)
file(GLOB_RECURSE SOURCES CONFIGURE_DEPENDS src/*.cpp)
add_executable(ProceduralAnimation ${SOURCES})

target_link_libraries(ProceduralAnimation PRIVATE sfml-graphics sfml-window sfml-system)

set_target_properties(ProceduralAnimation PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY_DEBUG   ${CMAKE_BINARY_DIR}/ProceduralAnimation/bin
    RUNTIME_OUTPUT_DIRECTORY_RELEASE ${CMAKE_BINARY_DIR}/ProceduralAnimation/bin
    RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_BINARY_DIR}/ProceduralAnimation/bin
    RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${CMAKE_BINARY_DIR}/ProceduralAnimation/bin
)

if(WIN32)
    add_custom_command(TARGET ProceduralAnimation POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_RUNTIME_DLLS:ProceduralAnimation> $<TARGET_FILE_DIR:ProceduralAnimation>
        COMMAND_EXPAND_LISTS
    )
endif()

add_custom_command(
    TARGET ProceduralAnimation POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            ${CMAKE_SOURCE_DIR}/resources
            ${CMAKE_BINARY_DIR}/ProceduralAnimation/resources
)